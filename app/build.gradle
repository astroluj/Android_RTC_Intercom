plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka-android'
/*apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.astroluj'*/
// apply from: file('publish.gradle')
    id 'maven-publish'
}
group = 'com.github.astroluj'

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    //File 불러오기
    def versionPropsFile = file('version.properties')

    def artifactName = "Android_RTC_NSIntercom"
    def versionMajor = 0
    def versionMinor = 0
    def versionPatch = 0
    def versionNumber = 0
    def versionBuild = 0

    //File Read 여부 체크
    if (versionPropsFile.canRead()) {
        def versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        //Release 할대만 VersionCode 및 patch 값을 Count up해 준다.
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            value = 1
        }

        // version info.
        versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        versionMinor = versionProps['VERSION_MINOR'].toInteger()
        versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value
        //Build 정보는 Debug, Release 상관없이 빌드하면 무조건 Count up
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_NUMBER'] = versionNumber.toString()

        //update 된 설정값들을 다시 version.properties 파일에 쓰기
        versionProps.store(versionPropsFile.newWriter(), null)
        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 33
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch}_Release"
            archivesBaseName = artifactName
        }

        android.libraryVariants.all { variant -> variant.outputs.all { output -> outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}.aar" } }
    } else {
        throw new Exception("Could not read version.properties!")
    }

    // Jni Setting
    sourceSets.main {
        jni.srcDirs = ['libs']
        jniLibs.srcDirs = ['src/main/jniLibs']
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}
// Make document
tasks.dokka {
    moduleName = 'RTC_NSIntercom'
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
    sourceDirs = files('src/main/java')
    skipEmptyPackages = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation files('libs/google-webrtc-1.0.32006.jar')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.astroluj'
                artifact(sourcesJar)
                artifactId = 'Android_RTC_NSIntercom'
                version = '1.2.12'
            }
        }
    }
}
