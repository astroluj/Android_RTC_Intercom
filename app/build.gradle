plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka-android'
    id 'maven-publish'
}
group = 'com.github.astroluj'

android {
    compileSdkVersion 32

    //File 불러오기
    def versionPropsFile = file('version.properties')

    //File Read 여부 체크
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        //Release 할대만 VersionCode 및 patch 값을 Count up해 준다.
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            value = 1
        }

        def versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        def versionMinor = versionProps['VERSION_MINOR'].toInteger()
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value
        //Build 정보는 Debug, Release 상관없이 빌드하면 무조건 Count up
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_NUMBER'] = versionNumber.toString()

        //update 된 설정값들을 다시 version.properties 파일에 쓰기
        versionProps.store(versionPropsFile.newWriter(), null)
        defaultConfig {
            namespace "com.astroluj.intercom"
            minSdkVersion 19
            targetSdkVersion 32
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch}_Release"
            archivesBaseName = "Android_RTC_Intercom"
        }
    } else {
        throw new Exception("Could not read version.properties!")
    }

    // Jni Setting
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDirs = ['src/main/jniLibs']
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// output file name fixed
android.libraryVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}.aar"
    }
}

// Make document
tasks.dokka {
    moduleName = 'RTC_Intercom'
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
    sourceDirs = files('src/main/java')
    skipEmptyPackages = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation files('libs/google-webrtc-1.0.32006.jar')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.astroluj'
                artifactId = 'Android_RTC_Intercom'
                version = '1.3.8'
            }
        }
    }
}
